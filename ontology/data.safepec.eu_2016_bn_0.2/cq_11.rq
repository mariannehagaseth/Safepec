PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX spc: <http://data.safepec.eu/2016/bn/0.2/>
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Competency Question 11:
# Which is the state_value for a given bn_node (class) and given lookup_parameter_values?
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#
SELECT DISTINCT ?bn_node_class ?state_class ?state_value
WHERE 
{
  VALUES (?bn_node_class ?lookup_param_val) {
    ( spc:class_corr_nd "true"^^xsd:string )
  } .
  #
  # get the state_class, which applies to the bn_node (CQ 06)
  ?state_class owl:equivalentClass|rdfs:subClassOf ?a .
  ?a owl:intersectionOf*/(rdf:rest*/rdf:first)* ?restriction .
  ?restriction rdf:type owl:Restriction .
  ?restriction owl:onProperty ?on_property .
  ?restriction owl:someValuesFrom|owl:allValuesFrom ?bn_node_class .
  FILTER ( ?on_property = spc:appliesTo ) .
  #
  # get the state_values and encoding lookup_params (CQ 12)
  _:state_value rdf:type ?state_class .
  _:state_value spc:state_value ?state_value .
  _:state_value spc:dependsOn _:lookup_param .
  _:lookup_param rdf:type ?lookup_param_class .
  _:lookup_param spc:lookup_parameter_value ?lookup_param_value .
  FILTER ( ?lookup_param_class != owl:NamedIndividual ) .
  # filter on given lookup_param_value
  #
  FILTER ( ?lookup_param_value = ?lookup_param_val ) .
}

