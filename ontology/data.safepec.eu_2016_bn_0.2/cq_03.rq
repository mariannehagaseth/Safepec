PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX spc: <http://data.safepec.eu/2016/bn/0.2/>
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Competency Question 3:
# What is the data_item_value for a given data_item of a given vessel?
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
SELECT DISTINCT ?vessel ?data_item_class ?data_item_value
WHERE 
{
  # query parameters
  VALUES ( ?imo ?data_item_class ) { ("9173317" spc:iacs_status) } .
  # assert that data_item_class is in subclass hierarchy
  # of spc:data_item
  ?data_item_class rdfs:subClassOf* spc:data_item .
  # get the vessel
  ?vessel rdf:type spc:vessel .
  ?vessel spc:imo_number ?imo_value .
  FILTER regex(str(?imo_value), ?imo, "i") .
  # get vessels data_items
  ?vessel spc:hasDataItem ?data_item_value .
  # get class of available data_items
  ?data_item_value rdf:type ?data_item_class .
}

