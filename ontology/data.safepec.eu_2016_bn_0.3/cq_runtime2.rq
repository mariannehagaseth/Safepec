PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX spc: <http://data.safepec.eu/2016/bn/0.3/>
PREFIX ccm: <http://data.safepec.eu/2016/bn/0.3/causes_corrosion/>
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Competency Question runtime:
#
# Get the result set of parameter label and value for a given vessel
# and data_integration_definition_class!
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
SELECT DISTINCT ?imo_number ?did_class ?data_item_class ?param_label ?param_value 
WHERE 
{
  VALUES (?model_class ?vessel_imo) {
    ( ccm:coating_lifetime "14575" )
  } .
  #
  # on TBox
  #
  # find all did_classes
  ?dependent_class rdfs:subClassOf+ spc:dependent .
  #
  {
    ?model_class owl:equivalentClass|rdfs:subClassOf+ ?dependent_class .

    # find all data_integration_defintions (class) which are restricted
    # to ?model_class by hasSubmodel object property
    ?dependent_class owl:equivalentClass|rdfs:subClassOf*
      [ owl:intersectionOf*/(rdf:rest*/rdf:first)* [
          rdf:type owl:Restriction ;
          owl:onProperty ?on_property3 ;
          owl:someValuesFrom|owl:allValuesFrom ?did_class
      ] ] .

    FILTER ( ?on_property3 = spc:isDependentOn ) .
  }
  UNION
  {
    ?model_class rdfs:subClassOf spc:data_integration_definition .
    BIND ( ?model_class AS ?did_class )
  }
  ?did_class rdfs:subClassOf+ spc:data_integration_definition .
  #  
  # return all classes which are subclasses of parameter_definition
  ?param_def_class rdfs:subClassOf+ spc:parameter_definition .
  #
  # select those classes, which have a restriction along isParameterDefinitionFor ?did_class
  ?param_def_class owl:equivalentClass|rdfs:subClassOf ?a .
  ?a owl:intersectionOf*/(rdf:rest*/rdf:first)* ?r1 .
  ?r1 rdf:type owl:Restriction .
  ?r1 owl:onProperty ?on_property1 .
  FILTER ( ?on_property1 = spc:isParameterDefinitionFor ) .
  # select those classes with the restriction applying to some values of ?did_class
  ?r1 owl:someValuesFrom|owl:allValuesFrom ?did_class .
  #
  # identify the subclasses of data_item, which apply to a restriction of scope on ?param_def_class
  ?param_def_class owl:equivalentClass|rdfs:subClassOf ?b .
  ?b owl:intersectionOf*/(rdf:rest*/rdf:first)* ?r2 .
  ?r2 rdf:type owl:Restriction .
  ?r2 owl:onProperty ?on_property2 .
  FILTER ( ?on_property2 = spc:scope ) .
  # select those classes with the restriction applying to some values of ?data_item_class
  ?r2 owl:someValuesFrom|owl:allValuesFrom ?data_item_class .
  #
  # on ABox
  #
  # find all parameter_definitions individuals of class ?param_def_class
  ?param_def_indiv rdf:type ?param_def_class .
  #
  # find all data_items in the scope of ?param_def_indiv for ?vessel_indiv
  ?param_def_indiv spc:scope ?data_item_indiv1 .
  #
  # find the vessel individual
  ?vessel_indiv rdf:type spc:vessel .
  ?vessel_indiv spc:imo_number ?imo_number .
  FILTER ( ?imo_number = ?vessel_imo ) .
  #
  ?vessel_indiv spc:hasDataItem ?data_item_indiv2 .
  ?data_item_indiv2 rdf:type ?data_item_class .
  #
  # apply lookup procedure, e.g. equality
  FILTER ( ?data_item_indiv1 = ?data_item_indiv2 ) .
  #
  # get the result set, i.e.,
  # find all parameters defined for ?param_def_indiv
  ?param_def_indiv spc:hasProperPart ?param_indiv .
  OPTIONAL
  {
    ?param_indiv spc:spc_label ?param_label .
    ?param_indiv spc:hasValue ?param_value .
  }
}



