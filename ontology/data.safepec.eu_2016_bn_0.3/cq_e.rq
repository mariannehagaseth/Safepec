PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX spc: <http://data.safepec.eu/2016/bn/0.3/>
PREFIX ccm: <http://data.safepec.eu/2016/bn/0.3/causes_corrosion/>
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Competency Question e:
#
# Which parameter_definitions (indiv.) are individuals to a
# parameter_definition (class) sub-class and have a vessel's
# data_item (indiv.) as their scope?
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
SELECT DISTINCT ?param_def_indiv ?imo_number ?data_item_indiv
WHERE 
{
  VALUES (?param_def_class ?vessel_imo) {
    ( ccm:class_pd "14575" )
  } .
  #
  # find the vessel individual
  ?vessel_indiv rdf:type spc:vessel .
  ?vessel_indiv spc:imo_number ?imo_number .
  FILTER ( ?imo_number = ?vessel_imo ) .
  #
  # find all parameter_definitions of class ?param_def_class
  ?param_def_indiv rdf:type ?param_def_class .
  #
  # find all data_items of ?vessel_indiv in the scope of ?param_def_indiv
  ?param_def_indiv spc:scope ?data_item_indiv .
  ?vessel_indiv spc:hasDataItem ?data_item_indiv .
  #
  # ensure consistency with the scope restriction between
  # ?param_def_class and ?data_item_class
  ?data_item_indiv rdf:type ?data_item_class .
  ?param_def_class owl:equivalentClass|rdfs:subClassOf ?b .
  ?b owl:intersectionOf*/(rdf:rest*/rdf:first)* ?r2 .
  ?r2 rdf:type owl:Restriction .
  ?r2 owl:onProperty ?on_property2 .
  FILTER ( ?on_property2 = spc:scope ) .
  # select those classes with the restriction applying to some values of
  # ?data_item_class
  ?r2 owl:someValuesFrom|owl:allValuesFrom ?data_item_class .
  #  
}
