PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX spc: <http://data.safepec.eu/2016/bn/0.3/>
PREFIX ccm: <http://data.safepec.eu/2016/bn/0.3/causes_corrosion/>
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# Competency Question c:
#
# Which data_items (class) are in the scope of a parameter_defintion (class)?
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
SELECT DISTINCT ?param_def_class ?data_item_class # ?data_item_class_label
WHERE 
{
  VALUES ?param_def_class { ccm:class_pd } .
  #
  # return all classes which are subclasses of data_item
  # => obsolete due to range of spc:scope
  # ?data_item_class rdfs:subClassOf+ spc:data_item .
  #
  # select those classes, which have a restriction along scope
  ?param_def_class owl:equivalentClass|rdfs:subClassOf ?a .
  ?a owl:intersectionOf*/(rdf:rest*/rdf:first)* ?restriction .
  ?restriction rdf:type owl:Restriction .
  ?restriction owl:onProperty ?on_property .
  FILTER ( ?on_property = spc:scope ) .
  # select those classes with the restriction applying to some values of ?data_item_class
  ?restriction owl:someValuesFrom|owl:allValuesFrom ?data_item_class .
  #
  #
  OPTIONAL
  {
    # provide the label, if applicable
    ?data_item_class spc:spc_label ?data_item_class_label .
  }
}
